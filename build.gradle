plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.21'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group = 'dev.sora.relay'
version = '1.1.1'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.opencollab.dev/maven-releases/' }
    maven { url = 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url = 'https://mccheatz.github.io/maven_repo/' }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed('com.nukkitx.protocol:bedrock-v560:2.9.15-SNAPSHOT') {
        exclude group: 'com.nukkitx.network'
        exclude group: 'com.nukkitx', module: 'natives'
    }
    embed project(':raknet')
    embed project(':natives')

    embed 'com.google.code.gson:gson:2.10'
    embed 'io.netty:netty-transport:4.1.86.Final'
    embed 'com.google.code.findbugs:jsr305:3.0.2'
    embed 'coelho.msftauth:minecraft-msftauth:2.1.2'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.embed]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}